apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: '../shared.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30

    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "com.programmergabut.solatkuy"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 63
        versionName "5.2"
        multiDexEnabled true

        //signingConfig signingConfigs.release
        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.programmergabut.solatkuy.HiltTestRunner"

        buildConfigField "String" , "BASE_URL_ALADHAN", base_url_aladhan
        buildConfigField "String" , "BASE_URL_QURAN_API", base_url_quran_api

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation" : "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        String sharedTestJavaDir = 'src/sharedTest/java'

        test {
            java.srcDirs += [sharedTestJavaDir]
        }
        androidTest {
            java.srcDirs += [sharedTestJavaDir]
            resources.srcDirs += ['src/test/resources']
        }
    }

    buildFeatures {
         viewBinding true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }


    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    dynamicFeatures = [':quran']
}

dependencies {

    api fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'androidx.appcompat:appcompat:1.3.0'
    api 'androidx.core:core-ktx:1.6.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api 'androidx.legacy:legacy-support-v4:1.0.0'

    // Material Dependencies
    api 'com.google.android.material:material:1.4.0'

    // Firebase
    api platform('com.google.firebase:firebase-bom:28.2.1')
    api 'com.google.firebase:firebase-analytics-ktx'
    api 'com.google.firebase:firebase-analytics-ktx:19.0.0'
    api 'com.google.firebase:firebase-crashlytics-ktx:18.1.0'

    //Shimmer
    api 'com.facebook.shimmer:shimmer:0.5.0'

    //Lottie
    implementation 'com.airbnb.android:lottie:3.6.0'
    implementation 'androidx.cardview:cardview:1.0.0'


    // Glide
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // JodaTime
    implementation 'joda-time:joda-time:2.10.5'

    // Work Manager
    api "androidx.work:work-runtime-ktx:2.5.0"

    // Room
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    // Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"
    api 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"

    /* ViewModel Kotlin support */
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    api "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'

    // Lifecycle
    api "androidx.recyclerview:recyclerview:1.2.1"
    api 'android.arch.lifecycle:extensions:1.1.1'

    // Retrofit
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api "com.squareup.okhttp3:logging-interceptor:4.9.1"

    // Gson
    api 'com.google.code.gson:gson:2.8.6'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Toasty
    api 'com.github.GrenderG:Toasty:1.4.2'

    // GPS
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // Play Core
    api "com.google.android.play:core:1.3.4"

    // Multidex
    api 'androidx.multidex:multidex:2.0.1'

    // Navigation Component
    androidTestApi "androidx.navigation:navigation-testing:$nav_version"
    api "androidx.navigation:navigation-fragment-ktx:$nav_version"
    api "androidx.navigation:navigation-ui-ktx:$nav_version"

    // espresso idling source
    api "androidx.test.espresso:espresso-idling-resource:3.4.0"
    api "com.android.support.test.espresso:espresso-idling-resource:3.0.2"

}
